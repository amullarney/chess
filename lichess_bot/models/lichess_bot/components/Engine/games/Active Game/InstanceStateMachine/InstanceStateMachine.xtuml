// BP 7.1.6 content: InstanceStateMachine syschar: 3 persistence-version: 7.1.6

within lichess_bot::components::Engine::games::'Active Game' is

  @state_num(1);
  @dialect("oal");
  state created;

  @state_num(3);
  @dialect("oal");
  state 'game over';

  @state_num(4);
  @dialect("oal");
  state 'our turn';

  @state_num(5);
  @dialect("oal");
  state 'their turn';

  @state_num(6);
  @dialect("oal");
  state 'playing move';

  @state_num(7);
  @dialect("oal");
  state 'claiming victory';

  @event_num(1);
  event 'waiting on move';

  @event_num(2);
  event 'game over'(status: in GameStatus);

  @event_num(3);
  event 'play move'(move: in string);

  @event_num(4);
  event 'ready to play';

  @event_num(5);
  event 'claim victory';

  state model is

    |                    | 'waiting on move'  | 'game over'        | 'play move'        | 'ready to play'    | 'claim victory'    |
    | ------------------ | ------------------ | ------------------ | ------------------ | ------------------ | ------------------ |
    | created            | 'their turn'       | cannot_happen      | cannot_happen      | 'our turn'         | ignore             |
    | 'game over'        | ignore             | cannot_happen      | ignore             | ignore             | ignore             |
    | 'our turn'         | ignore             | 'game over'        | 'playing move'     | 'our turn'         | 'claiming victory' |
    | 'their turn'       | ignore             | 'game over'        | ignore             | 'our turn'         | 'claiming victory' |
    | 'playing move'     | 'their turn'       | 'game over'        | cannot_happen      | ignore             | 'claiming victory' |
    | 'claiming victory' | ignore             | 'game over'        | ignore             | ignore             | ignore             |

  end state model;

  state 'game over'(status: in GameStatus) is
    @noparse
    LOG::LogInfo(message:"Game: " + self.id + ": Game over!");
    r = TIM::timer_cancel(timer_inst_ref: self.claim_victory_timer);
    self.dispose();
    @endnoparse
  end state;

  state 'our turn' is
    @noparse
    LOG::LogInfo(message: "Bot is choosing a move");
    
    played_moves = self.game_state.moves;
    
    
    select one myplayer related by self->Player[R22.'is acting for'];
    select one opponent related by myplayer->Player[R8.'opposes'];
    
    //select any opponent_fromocsq from instances of OccupiedSquare where false;  // decl
    //select any opponent_tosquare from instances of Square where false;  // decl
    
    i = played_moves.length;
    if (i > 0)
      // now update based on opponent's last move
      movestr = played_moves[i-1];  // most recent from opponent
      opponent_fromocsq = ::from_occupied( movestr:movestr );
      opponent_tosquare = ::to_square( movestr:movestr );
      ::record_move( from:opponent_fromocsq, to:opponent_tosquare, recorder:"player: " );
    end if;
    
    ::reconcile_moves( fen:self.initial_fen, moves:played_moves, player:myplayer );
    
    /*
    // aquire a list of legal moves to choose from
    legal_moves = ChessLib::legalMoves(fen: ChessLib::movesToFEN(initialFen: self.initial_fen, moves: played_moves));
    nlegals = legal_moves.length;
    
    // clean any move instances that are now no longer legal
    // (consider, for instance, check may vastly reduce legal moves)
    count = 0;
    select many potentialmoves related by myplayer->Move[R6.'considers'];
    for each potentialmove in potentialmoves
      select one start related by potentialmove->Piece[R5.'is reachable by']
                                                ->OccupiedSquare[R2.'occupies']->Square[R1];
      select one finish related by potentialmove->Square[R5.'can legally move to'];
      movestr = ::movestr( from:start, to:finish );
      i = 0;
      matched = false;
      while (i < nlegals)  // for each legal move we could play...
        if (movestr == legal_moves[i])
          matched = true;
          break;
        end if;
        i = i + 1;
      end while;
      if (not matched)
        count = count + 1;
        LOG::LogInfo(message: "Disposing non-legal move: " + movestr);
        potentialmove.dispose();
      end if;
    end for;
    LOG::LogInteger( message:count );
    LOG::LogInfo( message:"old moves disposed" );
    
    // start: a simplistic attempt to choose a move based on high-value target.
    select any chosen_move from instances of Move where false;  // decl
    
    // ensure that a move instance exists for each legal move; create any needed new ones
    count = 0;
    i = 0;
    while (i < nlegals)  // for each legal move we could play...
      movestr = legal_moves[i];
      fromocsq = ::from_occupied( movestr:movestr );
      tosq = ::to_square( movestr:movestr );
      select one piece related by fromocsq->Piece[R2.'is occupied by'];
      select many dests related by piece->Square[R5.'can legally move to'];
      found = false;
      for each dest in dests
        if (dest == tosq)
          found = true;
          break;
        end if;
      end for;
      if (not found)
        create object instance m of Move;
        m.Notation = movestr;  // for debug
        relate piece to tosq across R5 using m;
        relate m to myplayer across R6.'is considered by';
        count = count + 1;
      end if; 
      i = i + 1;
    end while;
    LOG::LogInteger( message:count );
    LOG::LogInfo( message:"new moves added" );
    
    // sanity check
    select many moves related by myplayer->Move[R6.'considers'];
    if (cardinality moves != nlegals)
      LOG::LogInfo(message: "Oops - non matched moves");
    end if;
    */
    
    // now we have bona-fide set of legal Move instances
    // let's pick the 'interesting' ones - those that have a target of value
    
    target_moves = ::find_target_moves( fen:self.initial_fen, moves: played_moves, player: myplayer );
    
    /*
    select many potentialtargets related by opponent->Piece[R7.'owns']
                                                    ->OccupiedSquare[R2.'occupies'];
    select many reachablesquares related by myplayer->Move[R6.'considers']
                                                    ->Square[R5.'can legally move to']
                                                    ->OccupiedSquare[R1];
    targets = potentialtargets & reachablesquares;
    
    // now, targets is the set of reachable squares occupied by opponent pieces
    
    best  = 0;
    select any chosen_move from instances of Move where false;  // decl
    select many moves related by targets->Square[R1]->Move[R5];
    for each move in moves
      select one spec related by move->Square[R5.'can legally move to']
                                          ->OccupiedSquare[R1]
                                          ->Piece[R2.'is occupied by']
                                          ->PieceSpecification[R4.'is specified by'];
       cost = ::evaluate_response( move:move, fen:self.initial_fen, played_moves: played_moves );
       move.Value = spec.Value - cost;  // the value of a piece less the price of response
       if (move.Value > best)
         best = move.Value;
         chosen_move = move;
       end if;
    end for;
    */
    best = 0;
    select any chosen_move from instances of Move where false;  // decl
    for each move in target_moves
      if (move.Value > best)
        best = move.Value;
        chosen_move = move;
      end if;
    end for;
    
    if (empty chosen_move)  
      // if no targetted move selected, may be opening stages of game; look for a move
      if (played_moves.length < 10)  // arbitrary limit on opening move search
    	  select many openermoves from instances of Move;
    	  chosen_move = ::pick_opener_move( moves:openermoves );
      end if;
      if (empty chosen_move)
        select any chosen_move from instances of Move;
      end if;
    end if;
    
    select one piece related by chosen_move->Piece[R5.'is reachable by'];
    select one tosq related by chosen_move->Square[R5.'can legally move to'];
    select one fromsq related by chosen_move->Piece[R5.'is reachable by']
                                            ->OccupiedSquare[R2.'occupies']->Square[R1];
    // play the move - in lichess notation
    ourmove = ::movestr( from:fromsq, to:tosq );
    LOG::LogInfo(message: "BOT selected: " + ourmove); 
    create event instance e of ActiveGame3:'play move'(move: ourmove) to self;
    // zero-timer prevents event prioritization by event-to-self acceleration.
    t = TIM::timer_start(event_inst:e, microseconds: 0);
    @endnoparse
  end state;

  state 'playing move'(move: in string) is
    @noparse
    movestr = param.move;
    
    // play the move
    LOG::LogInfo( message:"BOT asking to play move: " + movestr );
    success = chess::move(game_id: self.id, move: movestr);
    if success then
      fromocsq = ::from_occupied( movestr:movestr );
      tosquare = ::to_square( movestr:movestr );
      if ( (empty fromocsq) or (empty tosquare) )
        LOG::LogInfo( message:"Problem with: " + movestr );
      end if;
      ::record_move( from:fromocsq, to:tosquare, recorder:" BOT: " );
    	generate ActiveGame1:'waiting on move' to self;
    else
      LOG::LogFailure(message: "Failed to make move.");
      create event instance e of ActiveGame4:'ready to play' to self;
      t = TIM::timer_start(event_inst: e, microseconds: 100000);  // short delay to slow down endless loop possibility
    end if;
    @endnoparse
  end state;

  state 'claiming victory' is
    @noparse
    r = chess::claimVictory(game_id: self.id);
    @endnoparse
  end state;

  transition 'their turn' ['ready to play'] => 'our turn' is
    @noparse
    // handle takeback proposals
    if (self.color == Color::WHITE and self.game_state.btakeback) or
      (self.color == Color::BLACK and self.game_state.wtakeback) then
      self.handle_takeback_proposal();
    end if;
    
    // handle draw offers
    if (self.color == Color::WHITE and self.game_state.bdraw) or
      (self.color == Color::BLACK and self.game_state.wdraw) then
      self.handle_draw_offer();
    end if;
    @endnoparse
  end transition;

end;
