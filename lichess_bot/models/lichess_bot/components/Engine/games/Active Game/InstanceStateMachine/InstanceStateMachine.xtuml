// BP 7.1.6 content: InstanceStateMachine syschar: 3 persistence-version: 7.1.6

within lichess_bot::components::Engine::games::'Active Game' is

  @state_num(1);
  @dialect("oal");
  state created;

  @state_num(3);
  @dialect("oal");
  state 'game over';

  @state_num(4);
  @dialect("oal");
  state 'our turn';

  @state_num(5);
  @dialect("oal");
  state 'their turn';

  @state_num(6);
  @dialect("oal");
  state 'playing move';

  @state_num(7);
  @dialect("oal");
  state 'claiming victory';

  @event_num(1);
  event 'waiting on move';

  @event_num(2);
  event 'game over'(status: in GameStatus);

  @event_num(3);
  event 'play move'(move: in string);

  @event_num(4);
  event 'ready to play';

  @event_num(5);
  event 'claim victory';

  state model is

    |                    | 'waiting on move'  | 'game over'        | 'play move'        | 'ready to play'    | 'claim victory'    |
    | ------------------ | ------------------ | ------------------ | ------------------ | ------------------ | ------------------ |
    | created            | 'their turn'       | cannot_happen      | cannot_happen      | 'our turn'         | ignore             |
    | 'game over'        | ignore             | cannot_happen      | ignore             | ignore             | ignore             |
    | 'our turn'         | ignore             | 'game over'        | 'playing move'     | 'our turn'         | 'claiming victory' |
    | 'their turn'       | ignore             | 'game over'        | ignore             | 'our turn'         | 'claiming victory' |
    | 'playing move'     | 'their turn'       | 'game over'        | cannot_happen      | ignore             | 'claiming victory' |
    | 'claiming victory' | ignore             | 'game over'        | ignore             | ignore             | ignore             |

  end state model;

  state 'game over'(status: in GameStatus) is
    @noparse
    LOG::LogInfo(message:"Game: " + self.id + ": Game over!");
    r = TIM::timer_cancel(timer_inst_ref: self.claim_victory_timer);
    self.dispose();
    @endnoparse
  end state;

  state 'our turn' is
    @noparse
    played_moves = self.game_state.moves;
    
    select one myplayer related by self->Player[R22.'is acting for'];
    select one opponent related by myplayer->Player[R8.'opposes'];
    
    i = played_moves.length;
    if (i > 0)
      // now update based on opponent's last move
      movestr = played_moves[i-1];  // most recent from opponent
      opponent_fromocsq = ::from_occupied( movestr:movestr );
      opponent_tosquare = ::to_square( movestr:movestr );
      ::record_move( from:opponent_fromocsq, to:opponent_tosquare, recorder:"player: " );
    end if;
    
    // ensure that Move instances match allowed moves
    ::reconcile_moves( fen:self.initial_fen, moves:played_moves, player:myplayer );
    
    // now we have bona-fide set of legal Move instances find those that end on opponent squares
    target_moves = ::find_target_moves( player:myplayer );
    
    // evaluate the possible consequence of each such move - what might opponent capture?
    for each tgtmove in target_moves
      select one spec related by tgtmove->Square[R5.'can legally move to']->OccupiedSquare[R1]->Piece[R2.'is occupied by']->PieceSpecification[R4.'is specified by'];
      tgtmove.Value = spec.Value;
      ::evaluate_response( move:tgtmove, fen:self.initial_fen, played_moves: played_moves );
    end for;
    
    // select the move with the best trade-off - if there is one
    best = 0;
    select any chosen_move from instances of Move where false;  // decl
    for each tgtmove in target_moves
      if (tgtmove.Value > best)
        best = tgtmove.Value;
        chosen_move = tgtmove;
      end if;
    end for;
    
    if (empty chosen_move)  
      // if no targetted move selected, may be opening stages of game; look for a move
      if (played_moves.length < 10)  // arbitrary limit on opening move search
    	  select many openermoves related by myplayer->Move[R6.'considers'];
    	  chosen_move = ::pick_opener_move( moves:openermoves );
        LOG::LogInfo(message: "BOT picked opening move: " + chosen_move.Notation); 
      end if;
    end if;
    
    // nothing worthwhile to attack - consider vulnerablity avoidance
    if (empty chosen_move)
      chosen_move = ::play_safe( opponent:opponent );
    end if;
    
    // still no clear choice of move; time for some heuristic...
    if (empty chosen_move)
      if (not_empty target_moves)
    		for each move in target_moves
    		  if (move.Value > -2)  // in desperation, allow for minor loss
    		    chosen_move = move;
            LOG::LogInfo(message: "BOT picked sacrifice move: " + chosen_move.Notation);
    		    break;
    		  end if;
    		end for;
      end if;
    end if;
    
    // look for benign move
    if (empty chosen_move)
      select many mymoves related by myplayer->Move[R6.'considers'];
      select many reachables related by mymoves->Square[R5.'can legally move to']->OpenSquare[R1];
      select many coveredsquares related by opponent->Move[R6.'considers']->Square[R5.'can legally move to']->OpenSquare[R1];
      refuges = reachables - coveredsquares;
      select many tomoves related by refuges->Square[R1]->Move[R5];
      escapemoves = mymoves & tomoves;
      for each escape in escapemoves
        select one sq related by escape->Square[R5.'can legally move to'];
        LOG::LogInfo(message:"Random safe move to " + sq.lichess_desig);
        chosen_move = escape; 
        break;
      end for;
    end if;  
    
    // have to do something!
    if (empty chosen_move)
      select any chosen_move related by myplayer->Move[R6.'considers'];
      LOG::LogInfo(message: "BOT picked random move: " + chosen_move.Notation); // last resort!
    end if;
    
    select one piece related by chosen_move->Piece[R5.'is reachable by'];
    select one tosq related by chosen_move->Square[R5.'can legally move to'];
    select one fromsq related by chosen_move->Piece[R5.'is reachable by']
                                            ->OccupiedSquare[R2.'occupies']->Square[R1];
    // play the move - in lichess notation
    ourmove = ::movestr( from:fromsq, to:tosq );
    //LOG::LogInfo(message: "BOT selected: " + ourmove); 
    create event instance e of ActiveGame3:'play move'(move: ourmove) to self;
    // zero-timer prevents event prioritization by event-to-self acceleration.
    t = TIM::timer_start(event_inst:e, microseconds: 0);
    @endnoparse
  end state;

  state 'playing move'(move: in string) is
    @noparse
    movestr = param.move;
    
    // play the move
    //LOG::LogInfo( message:"BOT asking to play move: " + movestr );
    success = chess::move(game_id: self.id, move: movestr);
    if success then
      fromocsq = ::from_occupied( movestr:movestr );
      tosquare = ::to_square( movestr:movestr );
      if ( (empty fromocsq) or (empty tosquare) )
        LOG::LogInfo( message:"Problem with: " + movestr );
      end if;
      ::record_move( from:fromocsq, to:tosquare, recorder:" BOT: " );
    	generate ActiveGame1:'waiting on move' to self;
    else
      LOG::LogFailure(message: "Failed to make move.");
      create event instance e of ActiveGame4:'ready to play' to self;
      t = TIM::timer_start(event_inst: e, microseconds: 100000);  // short delay to slow down endless loop possibility
    end if;
    @endnoparse
  end state;

  state 'claiming victory' is
    @noparse
    r = chess::claimVictory(game_id: self.id);
    @endnoparse
  end state;

  transition 'their turn' ['ready to play'] => 'our turn' is
    @noparse
    // handle takeback proposals
    if (self.color == Color::WHITE and self.game_state.btakeback) or
      (self.color == Color::BLACK and self.game_state.wtakeback) then
      self.handle_takeback_proposal();
    end if;
    
    // handle draw offers
    if (self.color == Color::WHITE and self.game_state.bdraw) or
      (self.color == Color::BLACK and self.game_state.wdraw) then
      self.handle_draw_offer();
    end if;
    @endnoparse
  end transition;

end;
