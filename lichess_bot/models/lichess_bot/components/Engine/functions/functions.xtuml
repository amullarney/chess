// BP 7.1.6 content: Package syschar: 3 persistence-version: 7.1.6

within lichess_bot::components::Engine is

  package functions is
    @dialect("oal");
    @function_num(3);
    function init() is
      @noparse
      // load configuration properties
      select any bot from instances of Bot;
      if empty bot then
      	create object instance bot of Bot;
      	bot.config = PROP::loadProperties(file_name:"lichess_bot.properties");
      end if;
      
      Player::initialize();
      PieceSpecification::initialize();
      Square::initialize();
      Piece::initialize();
      Openers::initialize();
      
      // connect to the Lichess API and start listening for events
      send chess::connect(properties: bot.config);
      
      LOG::LogInfo(message: "Initialization complete");
      @endnoparse
    end function;

    //! Each move updates the positions of the pieces on the board, maintaining ground truth.
    @dialect("oal");
    @function_num(4);
    function record_move('from': in instance of OccupiedSquare, 'to': in instance of Square, recorder: in string) is
      @noparse
      start = param.from;
      dest = param.to;
      recorder = param.recorder;
      
      str = ::movestr( from:start, to:dest ); // debug
      LOG::LogInfo( message:"recording move from " + recorder + str );
      select one piece related by start->Piece[R2.'is occupied by'];
      select any ocsq from instances of OccupiedSquare where false;  // decl
      unrelate piece from start across R2.'occupies';
      select one square related by start->Square[R1];
      unrelate start from square across R1;
      delete object instance start;
      create object instance opsq of OpenSquare;
      relate opsq to square across R1;
      taken = dest.occupant();
      if (not_empty taken)
        select one ocsq related by dest->OccupiedSquare[R1];
        unrelate taken from ocsq across R2.'occupies';
        taken.clean_moves();
        delete object instance taken;
      else
        select one opsq related by dest->OpenSquare[R1];
        unrelate opsq from dest across R1;
        delete object instance opsq;
        create object instance ocsq of OccupiedSquare;
        relate ocsq to dest across R1;
      end if;
      relate piece to ocsq across R2.'occupies';
      // Castling? - the only time a King can move 2 squares along Rank 1 or 8.
      // lichess does not report the Rook's part in this; record it here to maintain ground truth.
      select one spec related by piece->PieceSpecification[R4.'is specified by'];
      if (spec.Name == "King")
      /*
        if (move.Notation == "e1g1")
          ::record_move( move:"h1f1", from:from + " O-O" );
        end if;
        if (move. == "e8g8")
          ::record_move( move:"h8f8", from:from + " O-O" );
        end if;
        // Queen side?
        if (move == "e1c1")
          ::record_move( move:"a1d1", from:from + " O-O" );
        end if;
        if (move == "e8c8")
          ::record_move( move:"a8d8", from:from + " O-O" );
        end if;
      */
      end if;
      @endnoparse
    end function;

    @dialect("oal");
    @function_num(1);
    function resign_all() is
      @noparse
      select many games from instances of ActiveGame;
      for each game in games 
      	game.resign();
      end for;
      @endnoparse
    end function;

    @dialect("oal");
    @function_num(2);
    function send_challenge() is
      @noparse
      r = chess::createChallenge(
      	user: "maia1",
      	rated: false,
      	clock_limit: 900,
      	clock_increment: 5,
      	color: Color::RANDOM,
      	variant: Variant::STANDARD,
      	fen: ""
      );
      @endnoparse
    end function;

  end package;

end;
