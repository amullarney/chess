// BP 7.1.6 content: Package syschar: 3 persistence-version: 7.1.6

within lichess_bot::components::Engine is

  package functions is
    @dialect("oal");
    @function_num(5);
    function from_square(move: in string) return instance of OccupiedSquare is
      @noparse
      str = param.move;
      move_from = STRING::substr(s:str, begin:0, end:2);
      select any square from instances of Square
       where (selected.lichess_desig == move_from);
      select one osq related by square->OccupiedSquare[R1];
      return osq;
      @endnoparse
    end function;

    @dialect("oal");
    @function_num(3);
    function init() is
      @noparse
      // load configuration properties
      select any bot from instances of Bot;
      if empty bot then
      	create object instance bot of Bot;
      	bot.config = PROP::loadProperties(file_name:"lichess_bot.properties");
      end if;
      
      PieceSpecification::initialize();
      Square::initialize();
      Piece::initialize();
      //Play::initialize();
      
      // connect to the Lichess API and start listening for events
      send chess::connect(properties: bot.config);
      
      LOG::LogInfo(message: "Initialization complete");
      @endnoparse
    end function;

    @dialect("oal");
    @function_num(4);
    function record_move(move: in string, 'from': in string) is
      @noparse
      move = param.move;
      from = param.from;
      LOG::LogInfo( message:"recording move from " + from + move );
      
      start = ::from_square( move:move );
      dest = ::to_square( move:move );
      select one square related by start->Square[R1];
      piece = square.occupant();
      unrelate piece from start across R2.'occupies';
      unrelate start from square across R1;
      delete object instance start;
      create object instance ops of OpenSquare;
      relate ops to square across R1;
      taken = dest.occupant();
      select any ocs from instances of OccupiedSquare where false;  // decl
      if (not_empty taken)
        select one ocs related by dest->OccupiedSquare[R1];
        unrelate taken from ocs across R2.'occupies';
        select one spec related by taken->PieceSpecification[R4.'is specified by'];
        LOG::LogInfo( message:"Capturing piece " + spec.Name + " on " + dest.lichess_desig );
        //delete object instance taken;
      else
        create object instance ocs of OccupiedSquare;
        relate ocs to dest across R1;
      end if;
      relate piece to ocs across R2.'occupies';
      // Castling? - the only time a King can move 2 squares along Rank 1 or 8.
      // lichess does not report the Rook's part in this; record it here to maintain ground truth.
      select one spec related by piece->PieceSpecification[R4.'is specified by'];
      if (spec.Name == "King")
        if (move == "e1g1")
          ::record_move( move:"h1f1", from:from + " O-O" );
        end if;
        if (move == "e8g8")
          ::record_move( move:"h8f8", from:from + " O-O" );
        end if;
        // Queen side?
        if (move == "e1c1")
          ::record_move( move:"a1d1", from:from + " O-O" );
        end if;
        if (move == "e8c8")
          ::record_move( move:"a8d8", from:from + " O-O" );
        end if;
      end if;
      @endnoparse
    end function;

    @dialect("oal");
    @function_num(1);
    function resign_all() is
      @noparse
      select many games from instances of ActiveGame;
      for each game in games 
      	game.resign();
      end for;
      @endnoparse
    end function;

    @dialect("oal");
    @function_num(2);
    function send_challenge() is
      @noparse
      r = chess::createChallenge(
      	user: "maia1",
      	rated: false,
      	clock_limit: 900,
      	clock_increment: 5,
      	color: Color::RANDOM,
      	variant: Variant::STANDARD,
      	fen: ""
      );
      @endnoparse
    end function;

    @dialect("oal");
    @function_num(6);
    function to_square(move: in string) return instance of Square is
      @noparse
      str = param.move;
      move_to = STRING::substr(s:str, begin:2, end:4);
      select any square from instances of Square
       where (selected.lichess_desig == move_to);
      return square;
      @endnoparse
    end function;

  end package;

end;
